///$tab Change Log
/*

0.0.8 - 19/10/15
----------------
* Initial version.

*/
///$tab Config
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='£#,##0.00;-£#,##0.00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';

// You can set this to your own app name.
LET vQVSourceAppId = 'Google_Webmaster_Tools_Demo_App';
	
// This could be updated to point to a different server / port.
LET vBaseUrl = 'http://localhost:5555';

// This could be &loadAccessToken=1a2b3c4d for QVSource web edition.
LET vLoadAccessToken = '';
///$tab ListSites
SUB listSites	

	TRACE 'Listing sites...';
	
	ListSites:
	LOAD
    	siteUrl as SiteUrl,
	    permissionLevel as ListSitesPermissionLevel
	FROM
		[$(vBaseUrl)/QVSource/GoogleWebmasterToolsConnector/?table=ListSites&appId=$(vQVSourceAppId)$(vLoadAccessToken)]
	(qvx);

ENDSUB
///$tab GetStats
SUB getStats

	LET noRows = NoOfRows('ListSites');
	
	TRACE 'Getting stats for $(noRows) sites...';
	
	FOR i=0 to $(noRows)-1

		LET site_url = peek('SiteUrl', $(i), 'ListSites');
		
		// Check the site is accessible
		Site:
		LOAD
		    siteUrl as SiteUrl,
		    permissionLevel as SitepermissionLevel,
		    status as Sitestatus
		FROM
			[$(vBaseUrl)/QVSource/GoogleWebmasterToolsConnector/?table=Site&siteUrl=$(site_url)&appId=$(vQVSourceAppId)$(vLoadAccessToken)]
		(qvx);
		
		// There should be exactly 1 row
		LET status = peek('Sitestatus', 0, 'Site');
		
		IF status='OK' THEN
		
			ByCountryAndDevice:
			LOAD
				'$(site_url)' as SiteUrl,
		    	dimension_country as ByCountryAndDeviceDimension_country,
			    dimension_device as ByCountryAndDeviceDimension_device,
			    clicks as ByCountryAndDeviceClicks,
			    impressions as ByCountryAndDeviceImpressions,
			    ctr as ByCountryAndDeviceCtr,
			    position as ByCountryAndDevicePosition
			FROM
				[$(vBaseUrl)/QVSource/GoogleWebmasterToolsConnector/?table=SearchAnalyticsQuery&siteUrl=$(site_url)&startDate=01-01-2015&endDate=13-10-2015&dimensions=country%2cdevice&rowLimit=1000&appId=$(vQVSourceAppId)$(vLoadAccessToken)]
			(qvx);
			
			ByPageAndQuery:
			LOAD
				'$(site_url)' as SiteUrl,
			    dimension_page as ByPageAndQueryDimension_page,
			    dimension_query as ByPageAndQueryDimension_query,
			    clicks as ByPageAndQueryClicks,
			    impressions as ByPageAndQueryImpressions,
			    ctr as ByPageAndQueryCtr,
			    position as ByPageAndQueryPosition
			FROM
				[$(vBaseUrl)/QVSource/GoogleWebmasterToolsConnector/?table=SearchAnalyticsQuery&siteUrl=$(site_url)&startDate=01-01-2015&endDate=13-10-2015&dimensions=page%2cquery&rowLimit=1000&appId=$(vQVSourceAppId)$(vLoadAccessToken)]
			(qvx);
			
			ByPage:
			LOAD
				'$(site_url)' as SiteUrl,
			    dimension_page as ByPageDimension_page,
			    clicks as ByPageClicks,
			    impressions as ByPageImpressions,
			    ctr as ByPageCtr,
			    position as ByPagePosition
			FROM
				[$(vBaseUrl)/QVSource/GoogleWebmasterToolsConnector/?table=SearchAnalyticsQuery&siteUrl=$(site_url)&startDate=01-01-2015&endDate=13-10-2015&dimensions=page&rowLimit=1000&appId=$(vQVSourceAppId)$(vLoadAccessToken)]
			(qvx);
		
		ENDIF
	
	NEXT

ENDSUB
///$tab Main
call listSites;
call getStats;